<AParams> ::= <Expr> <AParamsTail>
<AParams> ::= EPSILON node,e
<AParamsTail> ::= 'comma' <Expr> <AParamsTail> 
<AParamsTail> ::= EPSILON
<AddOp> ::= 'plus' node,plus
<AddOp> ::= 'minus' node,minus
<AddOp> ::= 'or' node,or
<ArithExpr> ::= <Term> <ArithExprTail>
<ArithExprTail> ::= <AddOp> <Term> tree,addop,3 <ArithExprTail>
<ArithExprTail> ::= EPSILON
<ArraySizeRept> ::= 'lsqbr' <NumInt> 'rsqbr' <ArraySizeRept> 
<ArraySizeRept> ::= EPSILON
<AssignOp> ::= 'assign'
<AssignStatTail> ::= <AssignOp> <Expr> tree,assignstmt,2
<ClassDecl> ::= 'class' 'id' node,id node,e <Inherit> tree,inherlist,e 'lcurbr' node,e <ClassDeclBody> tree,memblist,e 'rcurbr' 'semi' tree,classdecl,3 <ClassDecl>
<ClassDecl> ::= EPSILON
<ClassDeclBody> ::= <Visibility> <MemberDecl> tree,memberdecl,2 <ClassDeclBody> 
<ClassDeclBody> ::= EPSILON
<ClassMethod> ::= 'sr' 'id' node,id
<ClassMethod> ::= EPSILON node,e
<Expr> ::= node,e <ArithExpr> tree,arithexpr,e <ExprTail>
<ExprTail> ::= <RelOp> node,e <ArithExpr> tree,arithexpr,e tree,relexpr,3
<ExprTail> ::= EPSILON
<FParams> ::= <Type> 'id' node,id node,e <ArraySizeRept> tree,dimlist,e tree,fparam,3 <FParamsTail>
<FParams> ::= EPSILON
<FParamsTail> ::= 'comma' <Type> 'id' node,id node,e <ArraySizeRept> tree,dimlist,e tree,fparam,3 <FParamsTail>
<FParamsTail> ::= EPSILON
<Factor> ::= <FuncOrVar> 
<Factor> ::= 'intlit' node,intlit
<Factor> ::= 'floatlit' node,floatlit
<Factor> ::= 'stringlit' node,stringlit
<Factor> ::= 'lpar' <Expr> 'rpar' 
<Factor> ::= 'not' <Factor> tree,not,1
<Factor> ::= <Sign> <Factor> tree,sign,2
<Factor> ::= 'qm' 'lsqbr' <Expr> 'colon' <Expr> 'colon' <Expr> 'rsqbr' tree,expr,3
<FuncBody> ::= 'lcurbr' node,e <MethodBodyVar> tree,vardecllist,e node,e <StatementList> 'rcurbr' tree,statblock,e tree,funcbody,2
<FuncDecl> ::= 'func' 'id' node,id 'lpar' node,e <FParams> tree,fparamlist,e 'rpar' 'colon' <FuncDeclTail> 'semi' tree,funcdecl,3
<FuncDeclTail> ::= <Type>
<FuncDeclTail> ::= 'void' node,void
<FuncDef> ::= <Function> <FuncDef> 
<FuncDef> ::= EPSILON
<FuncHead> ::= 'func' 'id' node,id <ClassMethod> tree,scopespec,1 'lpar' node,e <FParams> tree,fparamlist,e 'rpar' 'colon' <FuncDeclTail> tree,funchead,4
<FuncOrAssignStat> ::= 'id' node,id <FuncOrAssignStatIdnest> 
<FuncOrAssignStatIdnest> ::= node,e <IndiceRep> tree,indicelist,e tree,variable,2 <FuncOrAssignStatIdnestVarTail>
<FuncOrAssignStatIdnest> ::= 'lpar' node,e <AParams> tree,aparamlist,e tree,fcall,2 'rpar' <FuncOrAssignStatIdnestFuncTail>
<FuncOrAssignStatIdnestFuncTail> ::= 'dot' 'id' node,id <FuncStatTail> tree,dot,2
<FuncOrAssignStatIdnestFuncTail> ::= EPSILON
<FuncOrAssignStatIdnestVarTail> ::= 'dot' 'id' node,id tree,dot,2 <FuncOrAssignStatIdnest>
<FuncOrAssignStatIdnestVarTail> ::= <AssignStatTail> 
<FuncOrVar> ::= 'id' node,id <FuncOrVarIdnest> 
<FuncOrVarIdnest> ::= node,e <IndiceRep> tree,indicelist,e tree,variable,2 <FuncOrVarIdnestTail>
<FuncOrVarIdnest> ::= 'lpar' node,e <AParams> tree,aparamlist,e 'rpar' tree,fcall,2 <FuncOrVarIdnestTail>
<FuncOrVarIdnestTail> ::= 'dot' 'id' node,id <FuncOrVarIdnest> tree,dot,2
<FuncOrVarIdnestTail> ::= EPSILON
<FuncStatTail> ::= node,e <IndiceRep> tree,indicelist,e tree,variable,2 'dot' 'id' node,id <FuncStatTail> tree,dot,2
<FuncStatTail> ::= 'lpar' node,e <AParams> tree,aparamlist,e 'rpar' tree,fcall,2 <FuncStatTailIdnest> tree,dot,2
<FuncStatTailIdnest> ::= 'dot' 'id' node,id <FuncStatTail> tree,dot,2
<FuncStatTailIdnest> ::= EPSILON
<Function> ::= <FuncHead> <FuncBody> tree,funcdef,2
<IndiceRep> ::= 'lsqbr' <Expr> 'rsqbr' <IndiceRep> 
<IndiceRep> ::= EPSILON
<Inherit> ::= 'inherits' 'id' node,id <NestedId> 
<Inherit> ::= EPSILON
<NumInt> ::= 'intlit' node,intlit
<NumInt> ::= EPSILON node,e
<MemberDecl> ::= <FuncDecl> 
<MemberDecl> ::= <VarDecl>
<MethodBodyVar> ::= 'var' 'lcurbr' <VarDeclRep> 'rcurbr'
<MethodBodyVar> ::= EPSILON
<MultOp> ::= 'mult' node,mult
<MultOp> ::= 'div' node,div
<MultOp> ::= 'and' node,and
<NestedId> ::= 'comma' 'id' node,id <NestedId> 
<NestedId> ::= EPSILON
<Prog> ::= node,e <ClassDecl> tree,classdecllist,e node,e <FuncDef> tree,funcdeflist,e 'main' <FuncBody> tree,funcdef,1 tree,prog,3
<RelOp> ::= 'eq' node,eq
<RelOp> ::= 'neq' node,neq
<RelOp> ::= 'lt' node,lt
<RelOp> ::= 'gt' node,gt
<RelOp> ::= 'leq' node,leq
<RelOp> ::= 'geq' node,geq
<START> ::= <Prog>
<Sign> ::= 'plus' node,plus
<Sign> ::= 'minus' node,minus
<StatBlock> ::= 'lcurbr' <StatementList> 'rcurbr'
<StatBlock> ::= <Statement>
<StatBlock> ::= EPSILON
<Statement> ::= <FuncOrAssignStat> 'semi' 
<Statement> ::= 'if' 'lpar' <Expr> 'rpar' 'then' node,e <StatBlock> tree,statblock,e 'else' node,e <StatBlock> tree,statblock,e 'semi' tree,ifstmt,3
<Statement> ::= 'while' 'lpar' <Expr> 'rpar' node,e <StatBlock> tree,statblock,e 'semi' tree,whilestmt,2
<Statement> ::= 'read' 'lpar' <Variable> 'rpar' 'semi' tree,readstmt,1
<Statement> ::= 'write' 'lpar' <Expr> 'rpar' 'semi' tree,writestmt,1
<Statement> ::= 'return' 'lpar' <Expr> 'rpar' 'semi' tree,returnstmt,1
<Statement> ::= 'break' 'semi' node,break
<Statement> ::= 'continue' 'semi' node,continue
<StatementList> ::= <Statement> <StatementList> 
<StatementList> ::= EPSILON
<Term> ::= <Factor> <TermTail> 
<TermTail> ::= <MultOp> <Factor> tree,multop,3 <TermTail> 
<TermTail> ::= EPSILON
<Type> ::= 'integer' node,integer
<Type> ::= 'float' node,float
<Type> ::= 'string' node,string
<Type> ::= 'id' node,id
<VarDecl> ::= <Type> 'id' node,id node,e <ArraySizeRept> tree,dimlist,e 'semi' tree,vardecl,3
<VarDeclRep> ::= <VarDecl> <VarDeclRep> 
<VarDeclRep> ::= EPSILON
<Variable> ::= 'id' node,id <VariableIdnest> 
<VariableIdnest> ::= node,e <IndiceRep> tree,indicelist,e tree,variable,2 <VariableIdnestTail>
<VariableIdnestTail> ::= 'dot' 'id' node,id <VariableIdnest> tree,dot,2 
<VariableIdnestTail> ::= EPSILON
<Visibility> ::= 'public' node,public
<Visibility> ::= 'private' node,private
<Visibility> ::= EPSILON node,e
